<!DOCTYPE html>
<html lang="en">
<head>
  <title>Stars Fluid</title>
  <meta charset="utf-8"/>
  <meta name="description" content="Stars Fluid Chrome Experiment">
  <meta name="keywords" content="JavaScript,WegGL,Fluid,Particles">
  <meta name="author" content="Johann Troendle">

  <style>
    html, body { margin: 0; padding: 0; background-color: #000; }
    canvas { position: absolute; top: 0; left: 0; z-index: 0; cursor: pointer; }
    #screenshot { position: absolute; width: 100%; height: 100%; z-index: 1; display: none; }
    #ce { position: absolute; z-index: 3; bottom: 10px; right: 10px }

    .hcenter { display: table; width:100%; height:100vh; }
    .vcenter { display: table-cell; width:100%; height:100%; text-align: center; vertical-align: middle; }

    #helper {
      width: 256px;
      height: 256px;
      background-image: url('/src/img/mouses.png');
      opacity: .95;
      display: inline-block;

      -webkit-animation: play 1.75s steps(3) infinite;
         -moz-animation: play 1.75s steps(3) infinite;
          -ms-animation: play 1.75s steps(3) infinite;
           -o-animation: play 1.75s steps(3) infinite;
              animation: play 1.75s steps(3) infinite;
    }

    @-webkit-keyframes play {
       from { background-position:    0px; }
         to { background-position: -768px; }
    }

    @-moz-keyframes play {
       from { background-position:    0px; }
         to { background-position: -768px; }
    }

    @-ms-keyframes play {
       from { background-position:    0px; }
         to { background-position: -768px; }
    }

    @-o-keyframes play {
       from { background-position:    0px; }
         to { background-position: -768px; }
    }

    @keyframes play {
       from { background-position:    0px; }
         to { background-position: -768px; }
    }
  </style>
  
  <script src="src/js/lib/rtt.gl.js"></script>
  <script src="src/js/lib/stage.gl.js"></script>
  <script src="src/js/lib/loader.gl.js"></script>

  <script src="src/js/motion.js"></script>
  <script src="src/js/fluid.js"></script>
  <script src="src/js/particles.js"></script>
  <script src="src/js/visualizer.js"></script>
</head>

<body>
  <img id="screenshot" src="src/img/starsfluid.png"/>
  <canvas id="canvas" width="512" height="512"></canvas>

  <div class="hcenter">
    <div class="vcenter">
      <div id="helper"></div>
    </div>
  </div>

  <!-- Fluid Shaders -->
  <script type="application/x-glsl" id="fluid-reset">
    precision highp float;
    varying vec2 vUv;
    void main() {
      gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
    }
  </script>

  <script type="application/x-glsl" id="fluid-motion">
    precision highp float;
    varying vec2 vUv;
    uniform sampler2D tSampler;
    uniform float ratio;
    uniform vec2 point;
    uniform float dye;
    uniform vec2 velocity;
    const float VELOCITY_RADIUS = 500.0;
    const float DYE_RADIUS = 2000.0;
    void main() {
      gl_FragColor = texture2D(tSampler, vUv);
      vec2 pos = vUv * vec2(ratio, 1.0);
      vec2 rPoint = point * vec2(ratio, 1.0);
      float gaussian = -dot(pos - rPoint, pos - rPoint);
      gl_FragColor.xy = velocity * exp(gaussian * VELOCITY_RADIUS) * 100.0;
      gl_FragColor.z = dye * exp(gaussian * DYE_RADIUS);
    }
  </script>

  <script type="application/x-glsl" id="fluid-solver">
    precision highp float;
    varying vec2 vUv;
    uniform sampler2D tSampler;
    uniform sampler2D motion;
    uniform float dt;
    uniform vec2 d;
    vec2 dx = vec2(d.x, 0);
    vec2 dy = vec2(0, d.y);
    const float v = 0.05;
    const float K = 0.15;
    const float CentralScale = 1.0/2.0;
    vec2 Directions[4];

    bool IsBoundary(vec2 uv) {
      return (uv.x <= d.x || uv.x > (1.0 - d.x) || uv.y <= d.y || uv.y > (1.0 - d.y));
    }

    vec3 bilerp(sampler2D t, vec2 pos) {
      vec3 x = texture2D(t, pos).xyz;
      vec3 x0 = texture2D(t, pos - dx).xyz;
      vec3 x1 = texture2D(t, pos + dx).xyz;
      vec3 y0 = texture2D(t, pos - dy).xyz;
      vec3 y1 = texture2D(t, pos + dy).xyz;
      vec3 res = (1.0 * x + x0 + x1 + y0 + y1) * 0.2;
      return res;
    }

    void main() {
      Directions[0] = vec2(1,0);
      Directions[1] = vec2(0,-1);
      Directions[2] = vec2(-1,0);
      Directions[3] = vec2(0,1);

      vec4 FC = texture2D(tSampler, vUv);
      vec3 FR = texture2D(tSampler, vUv + dx).xyz;
      vec3 FL = texture2D(tSampler, vUv - dx).xyz;
      vec3 FT = texture2D(tSampler, vUv + dy).xyz;
      vec3 FD = texture2D(tSampler, vUv - dy).xyz;

      vec2 Laplacian = FR.xy + FL.xy + FT.xy + FD.xy - 4.0 * FC.xy;
      vec3 UdX = (FR - FL) * CentralScale;
      vec3 UdY = (FT - FD) * CentralScale;
      vec2 Viscosity = v * Laplacian;
      vec2 DdX = vec2(UdX.z, UdY.z); 
      vec2 PdX = (K/dt) * DdX;
      vec3 Temp = vec3(DdX, UdX.x + UdY.y);
      FC.z = clamp(FC.z - dt * dot(FC.xyz, Temp), 0.3, 1.7);  
      vec2 Was = vUv - dt * FC.xy * d;
      FC.xy = bilerp(tSampler, Was).xy;
      FC.xy += dt * (Viscosity - PdX + texture2D(motion, vUv).xy);

      for (int i=0; i<4; i++) {
        if (IsBoundary(vUv + (d * Directions[i]))) { 
          FC.xy *= 1.0 - abs(Directions[i]);
        }
      } 

      gl_FragColor = FC;
    }
  </script>

  <script type="application/x-glsl" id="fluid-dye">
    precision highp float;
    varying vec2 vUv;
    uniform sampler2D tSampler;
    uniform sampler2D motion;
    uniform sampler2D solver;
    uniform float dt;
    uniform vec2 d;
    const float acc = 2.0;
    const float diffusion = 0.994;

    void main() {
      vec2 Was = vUv - dt * texture2D(solver, vUv).xy * d * acc;
      gl_FragColor.xyz = texture2D(tSampler, Was).xyz * diffusion;
      gl_FragColor.z += texture2D(motion, vUv).z;
      gl_FragColor.a = 1.0;
    }
  </script>

  <!-- Particle Shaders -->
  <script type="application/x-glsl" id="fragment-particle-data">
    precision highp float;
    varying vec2 vUv;
    uniform sampler2D tSampler;
    uniform sampler2D solver;
    uniform float dt;
    uniform vec2 d;

    void main() {
      vec4 data = texture2D(tSampler, vUv);
      data.xy += dt * texture2D(solver, data.xy).xy * d * 2.0;
      if (data.a > 0.0) {
        data.a -= 0.01;
      }
      else {
        data = vec4(-1);
      } 
      gl_FragColor = data;
    }
  </script>

  <script type="application/x-glsl" id="vertex-particles">
    precision highp float;
    attribute vec2 position;
    varying vec2 vUv;
    uniform sampler2D particleData;
    uniform float ratio;

    void main() {  
      vec4 data = texture2D(particleData, position);
      vec2 point = data.xy;
      float size = data.z;

      vUv = position;
      vec2 vPos = point * 2.0 - 1.0;

      gl_PointSize = size * ratio;
      gl_Position = vec4(vPos.x, vPos.y, 0, 1);
    }
  </script>

  <script type="application/x-glsl" id="fragment-particles">
    precision highp float;
    varying vec2 vUv;
    uniform float ratio;
    uniform sampler2D tSampler;
    const vec4 color = vec4(1.0, 0.5, 0.166, 0.66);

    void main() {
      gl_FragColor = color;
    }
  </script>

  <!-- Visualizer Shader -->
  <script type="application/x-glsl" id="fluid-visualizer">
    precision highp float;
    varying vec2 vUv;
    uniform sampler2D sampler;
    uniform sampler2D particles;
    const vec4 dyeColor = vec4(0.3, .5, 1.1, 1.0);

    void main() {
      float dye = texture2D(sampler, vUv).z;
      vec4 particlesTexture = texture2D(particles, vUv);
      gl_FragColor = dye * dyeColor + particlesTexture;
    }
  </script>

  <!-- Main JavaScript -->
  <script>
    (function() {
      var canvas = document.getElementById('canvas');

      var onResize = function() {
        canvas.width = window.innerWidth;
        canvas.height = window.innerHeight;
      };
      onResize();
      window.addEventListener('resize', onResize);

      var renderer, gl, supported = true;
      try {
        renderer = new Stage.Renderer(canvas);
        gl = renderer.context.gl;
      } catch(e) {
        supported = false;
      }

      supported = gl && gl.getExtension('OES_texture_float') && gl.getExtension('OES_texture_float_linear');

      if (!supported) {
        document.getElementById('screenshot').style.display = 'block';
        setTimeout(function() { 
          alert('Sorry, experiment is not supported on your browser.')
        }, 0);
        return;
      }
      
      gl.blendFunc(gl.SRC_ALPHA, gl.ONE);
      gl.clearColor(0.0, 0.0, 0.0, 1.0);

      renderer
        .effect(new Stage.Motion())
        .effect(new Stage.Fluid())
        .effect(new Stage.Particles())
        .effect(new Stage.Visualizer());

      Loader.done(function() {
        renderer.render();
      });

      document.addEventListener('keydown', function(e) {
        if (e.keyCode == 32) {
          renderer.pause();
        }
      }, false);

      document.addEventListener('mousedown', function(e) {
        document.getElementById('helper').style.display = 'none';
      }, false);
    })();
  </script>
</body>
</html>